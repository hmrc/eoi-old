


Option Compare Database   'Use database order for string comparisons

Option Explicit



Private Sub cmdCalculate_Click()
    
    DoCmd.Hourglass False 'just in case on when testing and crashed
    
    SAPSPPValid = True ' set valid = true and test after each call to the various checks.
                       ' If it's false then the nessage will already have been sent to
                       ' the user so just exit
    
    ' Check Matching Week date set
    
    If IsNull(Forms![frmSAPPayable]!txtMatchingWeek) Then
        Beep
        MsgBox ("Matching Week date not set"), vbInformation
        txtMatchingWeek.SetFocus
        Exit Sub
    End If

    ' Check Matching Week
    txtMatchingWeek_BeforeUpdate (0)
    If SAPSPPValid = False Then
        txtMatchingWeek.SetFocus
        Exit Sub
    End If
    
    ' Check Placement date set
    If IsNull(Forms![frmSAPPayable]!txtPlacement) Then
        Beep
        MsgBox ("Placement date not set"), vbInformation
        txtPlacement.SetFocus
        Exit Sub
    End If
    
    'check txt placement
    txtPlacement_BeforeUpdate (0)
    If SAPSPPValid = False Then
        txtPlacement.SetFocus
        Exit Sub
    End If

    ' Check pay period dates correct
    'txtIrregularTo_AfterUpdate
    SAPSPPCheckIrregularFromDate Me

    If SAPSPPValid = False Then
        txtIrregularFrom.SetFocus
        Exit Sub
    End If
    SAPSPPCheckIrregularToDate Me

    If SAPSPPValid = False Then
        txtIrregularTo.SetFocus
        Exit Sub
    End If
    'check Pay Period start date
    'Check date set
    If IsNull(Me!txtPeriodStarts) Then
        Beep
        MsgBox ("Pay Period Start date not set")
        txtPeriodStarts.SetFocus
        Exit Sub
    End If
    
    txtPeriodStarts_BeforeUpdate (0)
        If SAPSPPValid = False Then
        txtPeriodStarts.SetFocus
        Exit Sub
    End If

    ' Check earnings set
    CheckSAPSPPEarningsSet
    If SAPSPPValid = False Then
        Me!subEarnings.SetFocus
        Exit Sub
    End If

    'check earnings
    CheckSAPSPPEarningsDates
    If SAPSPPValid = False Then
        Me!subEarnings.SetFocus
        Exit Sub
    End If
    
    'clear totals fields
    SAPSPPReset Me
    
    'reset weekly fields
    
    'SetSAPSPPWeeklyDates Me, 26
    
    'Get average weekly earnings
    SetSAPSPPAverageEarnings Me, txtMatchingWeek
    
    txt5DPAverageWeeklyEarnings = Int((txtAverageWeeklyEarnings + 0.0000001) * 100000) / 100000 '+ 0.0000001 allows for numerical storage errors eg 74.975 is actually stored as 74.97499999 possibly
     
    'set rates
    setSAPRates
    
          'set under/over payments
    SetSAPSPPDueAndUnderOverPayment Me
    

End Sub

Private Sub cmdClear_Click()
            
    On Error GoTo Err_cmdClear_Click

    SAPSPPClear Me
    
Exit_cmdClear_Click:
    Exit Sub
    
Err_cmdClear_Click:
    MsgBox Error$
    Resume Exit_cmdClear_Click

End Sub

Private Sub cmdClose_Click()
' Author        : Mustaq Hussain
' Date          : 1 November 1995
' Fuction Name  : cmdClose_Click
' Purpose       : Run a macro which closes the SAP Payable form, back to main menu

    On Error GoTo Err_close_Click
    
    DoCmd.Close

Exit_close_Click:
    Exit Sub

Err_close_Click:
    MsgBox Error$
    Resume Exit_close_Click

End Sub

Private Sub cmdPrint_Click()
' Author        : Mustaq Hussain
' Date          : 17 May 1995
' Fuction Name  : cmdPrint_Click
' Purpose       : Run the report

    On Error GoTo Err_cmdPrint_Click

        cmdCalculate_Click 'ensure correct data in all fields
        
        DoCmd.OpenReport "rptSAPPayable", A_NORMAL
        'DoCmd.OpenReport "rptSAPPayable", acViewPreview 'pmor test
    

Exit_cmdPrint_Click:
    Exit Sub

Err_cmdPrint_Click:
    MsgBox Error$
    Resume Exit_cmdPrint_Click
    
End Sub

Private Sub cmdRecoverable_Click()
' Author        : Mustaq Hussain
' Date          : 1 December 1995
' Fuction Name  : cmdRecoverable_Click
' Purpose       : Close Payable and open Recoverable
' Author        : Pmor
' Date          : Jan 03
' Purpose       : Copy variables to recovery form before closing


    On Error GoTo Err_cmdRecoverable_Click
    DoCmd.SetWarnings False
    'clear subform data table
    DoCmd.RunSQL "DELETE * FROM [tblSAPPayments]"

    DoCmd.OpenForm "frmSAPRecoverable"
    With Forms!frmSAPRecoverable
        !txtName = txtName
        !txtInitials = txtInitials
        !NIPrefix = NIPrefix
        !NInumber = NInumber
        !NIsuffix = NIsuffix
        !txtRef = txtRef
        !txtPreparedBy = txtPreparedBy
        !subPayments.SetFocus
    End With
    DoCmd.Close acForm, "frmSAPPayable"
    
    DoCmd.SetWarnings True
Exit_cmdRecoverable_Click:
    Exit Sub

Err_cmdRecoverable_Click:
    MsgBox Error$
    Resume Exit_cmdRecoverable_Click
    
End Sub


Private Sub Form_Load()
' Author        : Mustaq Hussain
' Date          : 1 November 1995
' Fuction Name  : Form_Load
' Purpose       : Load the form

    On Error GoTo err_Form_Load
    
    
    Dim Sname As String
    Dim Fname As String
    
    DoCmd.SetWarnings True ' in case off if crashed when testing
    DoCmd.Maximize
    
    ' Set the prepared by field
    Forms![frmSAPPayable]!txtPreparedBy.Enabled = True
    
    Forms![frmSAPPayable]![txtPreparedBy] = getinitials() & " " & getsurname()
    Forms![frmSAPPayable]!txtPreparedBy.Enabled = False

    'reset fields
    SAPSPPClear Me
    
    'set combo fields on subform
    Me!subWeeklyPayments!Entitlement.RowSource = "SAP Due;Week Worked;Employee Excluded;SSP Paid;IR to Pay"


exit_Form_Load:
    Exit Sub

err_Form_Load:
    MsgBox Error$
    Resume exit_Form_Load

End Sub

Private Sub grpPayPeriod_AfterUpdate()

Dim strSQL As String
Dim rs As DAO.Recordset
Dim EntryCount

    On Error GoTo Err_grpPayPeriod_AfterUpdate
    
    'set the number of weeks or months in case the dates have been entered
    'but wrong period selected
    
    strSQL = "SELECT COUNT (*) AS CountOfRecords FROM tblSAPSPPEarnings"
    Set rs = CurrentDb.OpenRecordset(strSQL)
    EntryCount = rs!countofrecords
    
    If EntryCount = 0 Then
        EntryCount = ""
    End If
    
    Select Case grpPayPeriod
        Case gcPAY_PERIOD_WEEKLY
            txtNoOfWeeks = EntryCount
            txtNoOfMonths = ""
            txtIrregularFrom.Enabled = False
            txtIrregularTo.Enabled = False
        Case gcPAY_PERIOD_MONTHLY
            txtNoOfMonths = EntryCount
            txtNoOfWeeks = ""
            txtIrregularFrom.Enabled = False
            txtIrregularTo.Enabled = False

        Case gcPAY_PERIOD_IRREGULAR
            txtNoOfWeeks = ""
            txtNoOfMonths = ""
            txtIrregularFrom.Enabled = True
            txtIrregularTo.Enabled = True

    End Select
    
   
Exit_grpPayPeriod_AfterUpdate:
    Exit Sub

Err_grpPayPeriod_AfterUpdate:
    MsgBox Error$
    Resume Exit_grpPayPeriod_AfterUpdate

End Sub


Private Sub NIsuffix_AfterUpdate()
If Not IsNull(NIsuffix) And Not IsNull(txtMatchingWeek) Then

    Select Case NIsuffix
      
    
        Case "A" To "D", ""
        
        
            'do nowt
    
        Case "F", "M"
        
            If nTaxYear(txtMatchingWeek) > 2004 Then
                MsgBox "Only A, B, C or D allowed", vbOKOnly, "NI suffix"
                NIsuffix = Null
                txtMatchingWeek.SetFocus
                NIsuffix.SetFocus
            End If
                
        Case Else
        
            If nTaxYear(txtMatchingWeek) > 2004 Then
                MsgBox "Only A, B, C or D allowed", vbOKOnly, "NI suffix"
                NIsuffix = Null
                txtMatchingWeek.SetFocus
                NIsuffix.SetFocus
            Else
                MsgBox "Only A, B, C, D, F or M allowed", vbOKOnly, "NI suffix"
                NIsuffix = Null
                txtMatchingWeek.SetFocus
                NIsuffix.SetFocus
            End If
                  
    End Select

End If
End Sub

Private Sub txtIrregularFrom_AfterUpdate()
SAPSPPCheckIrregularFromDate Me
    If SAPSPPValid = False Then
        grpPayPeriod.SetFocus
        txtIrregularFrom.SetFocus
        
        Exit Sub
    End If
    
End Sub


Private Sub txtIrregularTo_AfterUpdate()
SAPSPPCheckIrregularToDate Me
    If SAPSPPValid = False Then
        Forms![frmSAPPayable]!grpPayPeriod.SetFocus
        Forms![frmSAPPayable]!txtIrregularTo.SetFocus
        Exit Sub
    End If
End Sub

Private Sub txtMatchingWeek_AfterUpdate()

NIsuffix_AfterUpdate

End Sub


Private Sub txtMatchingWeek_BeforeUpdate(Cancel As Integer)

If txtMatchingWeek < #4/8/2001# Then
    Cancel = True
    MsgBox "LEL data not available when matched week prior to 08/04/01", vbInformation
    SAPSPPValid = False 'used when checking form when Calculate clicked
    Exit Sub
End If


If Weekday(txtMatchingWeek) <> vbSunday Then
    Cancel = True 'keep the focus
    MsgBox "Matching Week must start on a Sunday", vbExclamation
    SAPSPPValid = False 'used when checking the whole form
End If

End Sub

Private Sub txtPeriodStarts_AfterUpdate()

If txtPlacement > #3/31/2007# Then

    SetSAPSPPWeeklyDates Me, 39
Else

    SetSAPSPPWeeklyDates Me, 26


End If

End Sub

Private Sub txtPeriodStarts_BeforeUpdate(Cancel As Integer)


If txtPeriodStarts < #4/6/2003# Then
    Cancel = True
    MsgBox "Pay Period must be on or after 06/04/2003", vbInformation
    SAPSPPValid = False 'used when checking form when Calculate clicked
    Exit Sub
End If


If DateAdd("d", 14, txtPeriodStarts) < txtPlacement Then
    Cancel = True 'keep the focus
    MsgBox "The APP Start Date is more than 2 weeks before the Date of Placement", vbExclamation
    SAPSPPValid = False 'used when checking the whole form
    Exit Sub
End If

If txtPeriodStarts > txtPlacement + 1 Then
    Cancel = True
    MsgBox "The APP Start Date cannot be more than 1 day after the Date of Placement"
    SAPSPPValid = False 'used when checking the whole form
    Exit Sub
End If


End Sub

Private Sub txtPlacement_BeforeUpdate(Cancel As Integer)

If txtPlacement < txtMatchingWeek Then
    Cancel = True
    MsgBox "Date of placement must be on or after Matching Week date", vbInformation
    SAPSPPValid = False 'used when checking form when Calculate clicked
    Exit Sub
End If

If txtPlacement < #4/6/2003# Then
    Cancel = True
    MsgBox "Date of placement must be on or after 06/04/2003", vbInformation
    SAPSPPValid = False 'used when checking form when Calculate clicked
    Exit Sub
End If

End Sub
